// Azure AD failed logon attempts 
// This further filters the results to show only failed sign-in attempts that have result type '50126' (Invalid username or password) or '50020' (The user doesn't exist in the tenant).
SigninLogs
| where TimeGenerated >= ago(1d)
| where ResultType in ('50126','50020') 
| extend OS = DeviceDetail.operatingSystem, 
         Browser = DeviceDetail.browser,
         StatusCode = tostring(Status.errorCode), 
         StatusDetails = tostring(Status.additionalDetails), 
         State = tostring(LocationDetails.state), 
         City = tostring(LocationDetails.city)
| summarize StartTime = min(TimeGenerated), 
         EndTime = max(TimeGenerated), 
         IPAddresses = make_set(IPAddress), 
         DistinctIPCount = dcount(IPAddress), 
         make_set(OS), make_set(Browser), make_set(City), 
         AttemptCount = count() by UserDisplayName, 
         UserPrincipalName, 
         AppDisplayName, 
         ResultType, 
         ResultDescription, 
         StatusCode, 
         StatusDetails, 
         Location, 
         State
| extend timestamp = StartTime, AccountCustomEntity = UserPrincipalName, IPADDR = tostring(IPAddresses[0])
| project timestamp,UserDisplayName, AppDisplayName, ResultType, IPADDR,AttemptCount,DistinctIPCount,ResultDescription